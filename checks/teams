# python-teams -- lintian check script -*- perl -*-
#
# Copyright Â© 2011 Jakub Wilk
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

package Lintian::teams;

use strict;
use warnings;

use Lintian::Tags qw(tag);

my %TEAMS = (
    dpmt => {
        name => 'Debian Python Modules Team',
        email => 'python-modules-team@lists.alioth.debian.org',
        vcs_browser => 'http://svn.debian.org/viewsvn/python-modules/packages/%s/trunk',
        vcs_svn => 'svn://svn.debian.org/python-modules/packages/%s/trunk'
    },
    papt => {
        name => 'Python Applications Packaging Team',
        email => 'python-apps-team@lists.alioth.debian.org',
        vcs_browser => 'http://svn.debian.org/viewsvn/python-apps/packages/%s/trunk',
        vcs_svn => 'svn://svn.debian.org/python-apps/packages/%s/trunk'
    }
);

sub run {

my ($pkg, $type, $info) = @_;
my %teams = ();
my @maintainers;
for (qw(maintainer uploaders)) {
    push @maintainers, split(m/\>\K\s*,\s*/, $info->field($_) // '');
}
for (@maintainers) {
    my ($name, $email) = /^\s*(\S|\S.*\S)\s*<(\S+)>\s*$/ or next;
    while (my ($team, $info) = each %TEAMS) {
        if ($email eq $info->{email}) {
            $teams{$team} = 1;
            if ($name ne $info->{name}) {
                tag 'incorrect-team-name', $name, '->', $info->{name};
            }
        }
    }
}
if ($type eq 'source' and (keys %teams) > 0) {
    my $vcs_browser = $info->field('vcs-browser');
    my $vcs_svn = $info->field('vcs-svn');
    if (not defined $vcs_browser) {
        tag 'missing-vcs-field', 'vcs-browser';
    }
    if (not defined $vcs_svn) {
        tag 'missing-vcs-field', 'vcs-svn';
    }
    foreach my $team (keys %teams) {
        $team = $TEAMS{$team};
        my $team_vcs_svn = sprintf $team->{vcs_svn}, $pkg;
        my $team_vcs_browser = sprintf $team->{vcs_browser}, $pkg;
        if (defined $vcs_svn and ($vcs_svn ne $team_vcs_svn) and ($vcs_svn ne "$team_vcs_svn/")) {
            tag 'incorrect-vcs-field', 'vcs-svn', $vcs_svn, '->', $team_vcs_svn;
        }
        if (defined $vcs_browser and ($vcs_browser ne $team_vcs_browser) and ($vcs_browser ne "$team_vcs_browser/")) {
            tag 'incorrect-vcs-field', 'vcs-browser', $vcs_browser, '->', $team_vcs_browser;
        }
    }

}

}

1;

# vim:syntax=perl ts=4 sw=4 et
