# python-extensions -- lintian check script -*- perl -*-
#
# Copyright Â© 2011 Jakub Wilk
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

package Lintian::extensions;

use strict;
use warnings;

use Lintian::Tags qw(tag);
use Util;

sub run {

my ($pkg, $type, $info, $proc) = @_;

if ($pkg =~ /-dbg$/) {
    # Don't bother checking *-dbg packages.
    return;
}

my $srcpkg = $proc->pkg_src() // '';
my $index = $info->index;
my $uses_numpy_abi = 0;
my $uses_old_pyrex_import_type = 0;
for my $file (@{$info->sorted_index}) {
    next if $index->{$file}->{link};
    if ($file =~ m,usr/lib/(?:python2\.\d+/(?:site|dist)-packages|pyshared/python2\.\d)/.*\.so$,) {
        my $string = slurp_entire_file("strings/$file");
        $uses_old_pyrex_import_type = ($uses_old_pyrex_import_type or
            $string =~ m,does not appear to be the correct type object,);
        $uses_numpy_abi = ($uses_numpy_abi or
            $string =~ m,module compiled against ABI version %x but this version of numpy is %x,);
    }
}
if ($uses_numpy_abi and ($srcpkg ne 'python-numpy')) {
    my $depends = $info->relation('strong')->unparse();
    tag 'missing-dependency-on-numpy-abi'
        unless
            $depends =~ m/(^|\s|,)python-numpy-(?:abi|api)/ or (
            $depends =~ m/(^|\s|,)python-numpy\s+\(>[>=]/ and
            $depends =~ m/(^|\s|,)python-numpy\s+\(<[<=]/);
}
if ($uses_old_pyrex_import_type) {
    tag 'extension-uses-old-pyrex-import-type';
}

}

1;

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
