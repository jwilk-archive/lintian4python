#!/usr/bin/perl
#
# refresh-python-projects -- Refresh Lintian data about Python project names
#
# Copyright Â© 2011, 2012, 2013 Jakub Wilk
#
# This program is free software.  It is distributed under the terms of the GNU
# General Public License as published by the Free Software Foundation; either
# version 2 of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
#
# You should have received a copy of the GNU General Public License along with
# this program.  If not, you can find it on the World Wide Web at
# http://www.gnu.org/copyleft/gpl.html, or write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301, USA.

use strict;
use warnings;

use v5.10; # for the // operator

use POSIX qw(strftime);

my $LINTIAN_ROOT = $ENV{'LINTIAN_ROOT'} // '.';
chdir $LINTIAN_ROOT or die $!;

my $mirror = $ENV{'DEB_MIRROR'} // 'http://http.debian.net/debian';
my @data = ({}, {});

my @urls = map { "$mirror/dists/$_/main/Contents-i386.gz" } qw(unstable experimental);
open(my $fh, "wget -q -O - @urls | zcat |") or die $!;
while (<$fh>) {
    if (m{^
        (?: (
            usr/lib/pyshared/python2\.\d+ | # python-support >= 0.90
            usr/share/pyshared | # python-support >= 0.90, python-central, dh_python2
            usr/lib/python-support/[^/]+/python2\.\d+ | # python-support < 0.90
            usr/share/python-support/[^/]+ | # python-support < 0.90
            usr/lib/python2\.\d+/(?:site|dist)-packages # python-central, dh_python2
        ) | (
            usr/lib/python3/dist-packages
        )) /
        (?: .*/ )? # FIXME: check if the directory is actually within sys.path
        ([.\w]+)-[^/]*\.egg-info
        (?: /.+ )?
        \s+
        (\S+)
        $
    }x) {
        my $py3k = defined $2;
        my $project_name = lc $3;
        my @package_names = split /,/, $4;
        map { s,.*/,, } @package_names;
        $data[$py3k]->{$project_name} //= {};
        $data[$py3k]->{$project_name}->{$_} = 1 foreach @package_names;
    }
}
close($fh) or die $!;
$data[0]->{'setuptools'} = $data[0]->{'distribute'};
$data[1]->{'setuptools'} = $data[1]->{'distribute'};
$data[0]->{'wsgiref'} = {'python (>= 2.5)' => 1, 'python-wsgiref' => 1};
$data[1]->{'wsgiref'} = {'python3' => 1};
$data[0]->{'argparse'} = {'python (>= 2.7)' => 1, 'python-argparse' => 1};
$data[0]->{'python'} = {'python' => 1};
$data[1]->{'python'} = {'python3' => 1};

my $date = strftime('%Y-%m-%d', gmtime);

for my $py3k (0..1) {
    my $python = sprintf 'Python %d.X', 2 + $py3k;
    open(my $fh, '>', sprintf('data/python%d-projects', 2 + $py3k)) or die $!;
    print $fh <<EOF ;
# This file contains mapping between $python project names (as used by
# setuptools) and Debian package names.
# It was automatically generated by private/refresh-python-projects.
#
# Last update: $date

EOF
    foreach my $project_name (sort keys %{$data[$py3k]}) {
        my @package_names = keys %{$data[$py3k]->{$project_name}};
        print $fh "$project_name||" . join(' | ', @package_names) . "\n";
    }
    close($fh) or die $!;

}

# Local Variables:
# indent-tabs-mode: nil
# End:
# vim: syntax=perl sw=4 sts=4 sr et
