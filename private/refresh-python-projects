#!/usr/bin/perl
#
# refresh-python-projects -- Refresh Lintian data about Python project names
#
# Copyright (C) 2011 Jakub Wilk
#
# This program is free software.  It is distributed under the terms of the GNU
# General Public License as published by the Free Software Foundation; either
# version 2 of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
#
# You should have received a copy of the GNU General Public License along with
# this program.  If not, you can find it on the World Wide Web at
# http://www.gnu.org/copyleft/gpl.html, or write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301, USA.

use strict;
use warnings;

use POSIX qw(strftime);

my $LINTIAN_ROOT = $ENV{'LINTIAN_ROOT'} // '.';
chdir $LINTIAN_ROOT or die $!;

my $mirror = $ENV{'DEB_MIRROR'} // 'http://i386-geomirror.debian.net/debian';
my @data = ({}, {});

open CONTENTS, "wget -q -O - $mirror/dists/unstable/main/Contents-i386.gz | zcat |" or die $!;
while (<CONTENTS>) {
    if (m{^
        (?: (
            usr/lib/pyshared/python2\.\d+ | # python-support >= 0.90
            usr/share/pyshared | # python-support >= 0.90, python-central, dh_python2
            usr/lib/python-support/[^/]+/python2\.\d+ | # python-support < 0.90
            usr/share/python-support/[^/]+ | # python-support < 0.90
            usr/lib/python2\.\d+/(?:site|dist)-packages # python-central, dh_python2
        ) | (
            usr/lib/python3/(?:site|dist)-packages
        )) /
        (\w+)-[^/]*\.egg-info
        (?: /.+ )?
        \s+
        (\S+)
        $
    }x) {
        my $py3k = defined $2;
        my $project_name = lc $3;
        my @package_names = split /,/, $4;
        map { s,.*/,, } @package_names;
        $data[$py3k]->{$project_name} //= {};
        $data[$py3k]->{$project_name}->{$_} = 1 foreach @package_names;
    }
}
close CONTENTS or die $!;
$data[0]->{'setuptools'} = $data[0]->{'distribute'};
$data[1]->{'setuptools'} = $data[1]->{'distribute'};

my $date = strftime('%Y-%m-%d', gmtime);

for my $py3k (0..1) {
    my $python = sprintf 'Python %d.X', 2 + $py3k;
    open PROJECTS, '>', 'data/python/projects' . ('-py3k' x $py3k) or die $!;
    print PROJECTS <<EOF ;
# This file contains mapping between $python project names (as used by
# setuptools) and Debian package names.
# It was automatically generated by private/refresh-python-projects.
#
# Last update: $date

EOF
    foreach my $project_name (sort keys %{$data[$py3k]}) {
        my @package_names = keys %{$data[$py3k]->{$project_name}};
        print PROJECTS "$project_name||" . join(' | ', @package_names) . "\n";
    }
    close PROJECTS or die $!;

}

# vim:ts=4 sw=4 et
